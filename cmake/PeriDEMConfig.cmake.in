@PACKAGE_INIT@

include(CMakeFindDependencyMacro)
list(APPEND CMAKE_MODULE_PATH "${CMAKE_CURRENT_LIST_DIR}")
find_dependency(MPI REQUIRED)
find_dependency(Threads REQUIRED)
find_dependency(yaml-cpp CONFIG REQUIRED)
find_dependency(VTK COMPONENTS CommonCore CommonDataModel IOXML)
find_dependency(BLAS REQUIRED)
find_dependency(LAPACK REQUIRED)
find_dependency(Metis REQUIRED)
# Provide fmt interface targets using the installed headers (or reuse an existing fmt if already present).
if(NOT TARGET fmt::fmt-header-only)
  add_library(fmt::fmt-header-only INTERFACE IMPORTED)
  get_filename_component(_peridem_prefix "${CMAKE_CURRENT_LIST_DIR}/../../.." ABSOLUTE)
  set_target_properties(fmt::fmt-header-only PROPERTIES
    INTERFACE_INCLUDE_DIRECTORIES "${_peridem_prefix}/include"
  )
endif()
if(NOT TARGET fmt::fmt)
  add_library(fmt::fmt INTERFACE IMPORTED)
  target_link_libraries(fmt::fmt INTERFACE fmt::fmt-header-only)
endif()

include("${CMAKE_CURRENT_LIST_DIR}/PeriDEMTargets.cmake")
